plugins {
	id 'org.springframework.boot' version '2.7.2'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
	id 'java'
}

group = 'io.jrb.labs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

ext {
	versions = [
			javatuples: "1.2",
			json_patch: "1.13",
	]
}

repositories {
	mavenCentral()
}

sourceSets {
	r2dbcSupport {
		compileClasspath += main.output
		runtimeClasspath += main.output
	}
}

java {
	registerFeature('r2dbcSupport') {
		usingSourceSet(sourceSets.r2dbcSupport)
	}
}

tasks.compileJava.finalizedBy compileR2dbcSupportJava
tasks.jar.finalizedBy r2dbcSupportJar

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation('org.springframework.boot:spring-boot-starter-hateoas') {
		exclude module: 'spring-boot-starter-web'
	}
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation "org.javatuples:javatuples:${versions.javatuples}"

	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.projectlombok:lombok'

	r2dbcSupportImplementation sourceSets.main.output
	r2dbcSupportImplementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	r2dbcSupportImplementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	r2dbcSupportImplementation "com.github.java-json-tools:json-patch:${versions.json_patch}"
	r2dbcSupportImplementation 'io.r2dbc:r2dbc-h2'
	r2dbcSupportCompileOnly 'org.projectlombok:lombok'
	r2dbcSupportAnnotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

bootJar {
	enabled = false
}

tasks.named('test') {
	useJUnitPlatform()
}
