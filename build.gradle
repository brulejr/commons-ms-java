plugins {
	id 'org.springframework.boot' version '2.7.2'
	id 'io.spring.dependency-management' version '1.0.12.RELEASE'
	id 'java'
}

group = 'io.jrb.labs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

ext {
	versions = [
			javatuples: "1.2",
			json_patch: "1.13",
	]
}

repositories {
	mavenCentral()
}

sourceSets {
	r2dbc {
		compileClasspath += main.output
		runtimeClasspath += main.output
	}
}

java {
	registerFeature('r2dbc') {
		usingSourceSet(sourceSets.r2dbc)
	}
}

tasks.compileJava.finalizedBy compileR2dbcJava
tasks.jar.finalizedBy r2dbcJar

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation('org.springframework.boot:spring-boot-starter-hateoas') {
		exclude module: 'spring-boot-starter-web'
	}
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation "org.javatuples:javatuples:${versions.javatuples}"

	compileOnly 'org.projectlombok:lombok'

	annotationProcessor 'org.projectlombok:lombok'

	r2dbcImplementation sourceSets.main.output
	r2dbcImplementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	r2dbcImplementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	r2dbcImplementation "com.github.java-json-tools:json-patch:${versions.json_patch}"
	r2dbcImplementation 'io.r2dbc:r2dbc-h2'
	r2dbcCompileOnly 'org.projectlombok:lombok'
	r2dbcAnnotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
}

bootJar {
	enabled = false
}

tasks.named('test') {
	useJUnitPlatform()
}
